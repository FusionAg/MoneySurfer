apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'pro.agfusion.plugin.dropbox'

android {
    signingConfigs {
        debug {
            keyAlias 'debug'
            keyPassword '123456'
            storeFile file('../ag-debug')
            storePassword '123456'
        }
        play {
            keyAlias 'release'
            storeFile file('../ag-prod')
            storePassword signing_keychain_password
            keyPassword signing_key_password
        }
    }
    compileSdkVersion target_sdk
    buildToolsVersion android_build_tools
    defaultConfig {
        manifestPlaceholders = [fabric_api_key: fabric_api_key]
        applicationId "pro.agfusion.moneysurfer"
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        versionCode app_version_code
        versionName app_version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'Boolean', 'enableCrashlytics', enable_fabric.toString()

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        testProguardFiles 'test-proguard-rules.pro'

        multiDexEnabled false
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
            archivesBaseName = "surfer-debug-${app_version}"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            archivesBaseName = "surfer-${app_version}"
            signingConfig enable_prod_signature ? signingConfigs.play : signingConfigs.debug
        }
    }
    lintOptions {
        abortOnError true
        textReport true
        warningsAsErrors true
    }
}

dropbox {
    code = dropbox_api_code
    token = dropbox_api_token

    upload << ['**build/outputs/apk/*.apk': "/report/$app_version_code/apk/"]
    upload << ['**build/reports/lint*.*': "/report/$app_version_code/lint/"]
    upload << ['**build/reports/tests/': "/report/$app_version_code/tests/"]
    upload << ['**build/reports/androidTests/': "/report/$app_version_code/androidTests/"]
    upload << ['**build/outputs/dexcount/': "/report/$app_version_code/dexcount/"]
}

play {
    jsonFile = file("../publisher.json")
    track = 'alpha'
}

dependencies {
    compile "com.android.support:support-v4:$android_support"
    compile "com.android.support:appcompat-v7:$android_support"
    compile "com.android.support:recyclerview-v7:$android_support"
    compile "com.android.support:design:$android_support"

    compile "com.android.support.constraint:constraint-layout:$android_constraint_layout"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    compile "io.reactivex.rxjava2:rxkotlin:$rx_kotlin"
    compile "io.reactivex.rxjava2:rxjava:$rx_java"
    compile "com.jakewharton.rxrelay2:rxrelay:$rx_relay"
    compile "com.trello.rxlifecycle2:rxlifecycle-android:$rx_android"
    compile "com.trello.rxlifecycle2:rxlifecycle-navi:$rx_android"
    compile "com.trello.rxlifecycle2:rxlifecycle-kotlin:$rx_android"
    compile "io.reactivex.rxjava2:rxandroid:$rx_android"

    compile "com.jakewharton.rxbinding2:rxbinding-kotlin:$rx_bindings"
    compile "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rx_bindings"
    compile "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rx_bindings"
    compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rx_bindings"
    compile "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rx_bindings"

    compile("com.crashlytics.sdk.android:crashlytics:$crashlytics_library@aar") { transitive = true }

    testCompile "junit:junit:$junit_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
    testCompile "org.hamcrest:hamcrest-all:$hamcrest_version"

    androidTestCompile "junit:junit:$junit_version"

    androidTestCompile "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-web:$espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espresso_version"
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        force "com.android.support:support-annotations:$android_support"
        force "com.google.code.findbugs:jsr305:$jsr305_version"

        force "com.android.support:support-annotations:$android_support"
        force "com.android.support:appcompat-v7:$android_support"
        force "com.android.support:recyclerview-v7:$android_support"
        force "com.android.support:design:$android_support"
        force "com.android.support:support-v4:$android_support"
    }
}

task('prepareFabricProps') << {
    if (enable_fabric) {
        def file = file('fabric.properties')
        file.text = "apiSecret=$fabric_api_secret"
    }
}

afterEvaluate {
    crashlyticsUploadDistributionDebug.dependsOn prepareFabricProps
    crashlyticsUploadDistributionDebug.dependsOn assembleDebug

    if (enable_fabric) {
        debugPublish.finalizedBy crashlyticsUploadDistributionDebug
    }
    if (enable_release_publish) {
        releasePublish.finalizedBy publishApkRelease
    }
    if (dropbox_api_enabled) {
        dropboxPublish.finalizedBy dropboxUpload
    }
}

task('debugPublish') {
    group "publishing"
    doLast {
        println "*** Fabric is ${enable_fabric ? 'enabled' : 'disabled'}"
    }
}

task('releasePublish') {
    group "publishing"
    doLast {
        println "*** Play publish is ${enable_fabric ? 'enabled' : 'disabled'}"
    }
}


task('dropboxPublish') {
    group "publishing"
    doLast {
        println "*** Play publish is ${enable_fabric ? 'enabled' : 'disabled'}"
    }
}